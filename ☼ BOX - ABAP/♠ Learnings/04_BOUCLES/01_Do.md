# **PROGRAMMATION**

## **DO ... ENDDO**

```ABAP
DO [n TIMES].  
  [statement_block]  
ENDDO.
```

Une **boucle** en programmation est un processus permettant de répéter une opération autant de fois que nécessaire. En **ABAP**, il en existe trois au total (une sera développée dans le chapitre Les tables internes - Organisation et Lecture) dont la première est le **DO… ENDDO**.

L’option **n TIMES** permet de définir combien de fois la boucle doit être répétée.

### **N TIME DEFINI**

```ABAP
DO 5 TIMES. 
  WRITE:/ sy-index. 
ENDDO.
```

La variable système **SY-INDEX** est un compteur et indique donc combien de fois la boucle a été exécutée. Ainsi le **DO** a été paramétré pour être exécuté cinq fois et à chaque fois, la variable **SY-INDEX** est affichée.

### **EXIT**

Il est très important de toujours définir une sortie à une boucle au risque de créer une boucle infinie. Dans cet exemple l’option n **TIMES** a été utilisée, mais il est également possible d’utiliser des instructions comme **EXIT**.

Ainsi en reprenant l’exemple ci-dessus, mais sans l’utilisation de **n TIMES** :

```ABAP
DO. 
  IF sy-index > 5. 
    EXIT. 
  ELSE. 
    WRITE:/ sy-index.  
  ENDIF. 
ENDDO.
```

### **CHECK**

Le **CHECK** est une condition comparant l’état d’une variable avec une autre. Si la condition est vraie alors le programme continue sinon il sort du traitement :

```ABAP
DO 5 TIMES. 
  CHECK sy-index < 3. 
  WRITE / sy-index. 
ENDDO.
```

Le CHECK peut très bien être comparé au IF... ENDIF et peut tout aussi bien être utilisé tel quel, hors d’une boucle.

```ABAP
DATA: v_month TYPE i. 
 
v_month = sy-datum+4(2). 
 
CHECK v_month GE 6. 
 
WRITE:/ 'Affichage des mois du second semestre'. 
  
CASE v_month.  
  WHEN 6. 
    WRITE:/'Mois de juin'. 
  WHEN 7. 
    WRITE:/'Mois de juillet'. 
  WHEN 8. 
    WRITE:/'Mois d''août'. 
  WHEN 9. 
    WRITE:/'Mois de septembre'. 
  WHEN 10. 
    WRITE:/'Mois d''octobre'. 
  WHEN 11. 
    WRITE:/'Mois de novembre'. 
  WHEN 12. 
    WRITE:/'Mois de décembre'.  
ENDCASE.
```

### **CONTINUE**

Le **CONTINUE** a un fonctionnement bien différent. Il est souvent utilisé dans une condition **IF** et si celle-ci est validée, alors le **CONTINUE** arrêtera le traitement pour passer à la ligne suivante.

```ABAP
DO 5 TIMES. 
  IF sy-index < 3. 
    CONTINUE. 
  ENDIF. 
  WRITE / sy-index. 
ENDDO.
```